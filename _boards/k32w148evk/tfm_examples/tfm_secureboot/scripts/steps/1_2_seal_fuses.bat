@echo off
@rem -- The script for fusing authentication key hash and SB3KDK. --

@rem -- Set variables. --
call ..\config.cmd
if errorlevel 1 goto ERROR

pushd "%SPT_WORKSPACE%"

choice /C NY /T 10 /D N /M "WARNING: Do you want to fuse the ROTKTH and SB3KDK (irreversible operation)."
if %ERRORLEVEL% == 1 goto WARNING

if %ERRORLEVEL% neq 2 goto EXIT

@rem -- Ping the device to establish communication. --
@echo ### Check connection ###
"%blhost%" %connect% -- get-property 1 0
if errorlevel 1 goto ERROR

@rem -- Read the ROTKTH from rotkth.txt. This is generated by provisioning script. --
FOR /F %%i IN (configs/rotkth.txt) do set hash=%%i	
set rotkth=%hash:~0,64%
@echo rotkth is %rotkth%

@rem -- Read the SB3KDK from sb3kdk.txt. --
FOR /F %%i IN (configs/sb3kdk.txt) do set key=%%i
set sb3kdk=%key:~0,64%
@echo sb3kdk is %sb3kdk%

@echo ### Increase voltage for fuse burning ###
"%blhost%" %connect% -- set-property 22 1
if errorlevel 1 goto ERROR

@echo ### Burn fuse: CUST_PROD_OEMFW_AUTH_PUK[0:255] ###
@echo blhost %connect% -- fuse-program 0x1F {{%rotkth%}}
"%blhost%" %connect% -- fuse-program 0x1F {{%rotkth%}}
if errorlevel 1 (
	@echo "Error in programming ROTKTH in CUST_PROD_OEMFW_AUTH_PUK"
	set issue=1
)
@echo ### Burn fuse: CUST_PROD_OEMFW_ENC_SK[0:255] ###
@echo blhost %connect% -- fuse-program 0x20 {{%sb3kdk%}}
"%blhost%" %connect% -- fuse-program 0x20 {{%sb3kdk%}}
if errorlevel 1 (
	@echo "Error in programming SB3KDK in CUST_PROD_OEMFW_ENC_SK"
	set issue=1
)

@echo ### Set voltage to normal value ###
"%blhost%" %connect% -- set-property 22 0
if errorlevel 1 goto ERROR

if [%issue%] == [1] goto ERROR

@echo ### Reset ###
"%blhost%" %connect% -- reset
@echo ### Delay until processor is restarted;###
setlocal EnableDelayedExpansion
SET /A "timeout=3 + 1"
ping -n !timeout! 127.0.0.1 >NUL
endlocal
@rem Ping the device to establish communication; Result is ignored as the communication sometime fails for the first time
"%blhost%" %connect% -- get-property 1 0 > nul 2> nul

@echo ### Check connection ###
"%blhost%" %connect% -- get-property 1 0
if errorlevel 2 goto ERROR

@echo SUCCESS
:EXIT
    popd
	if not defined NO_PAUSE (pause)
	exit /b 0
	
:WARNING
	popd
	if not defined NO_PAUSE (pause)
	exit /b 1
	
:ERROR
@echo FAIL
    popd
    if not defined NO_PAUSE (pause)
    exit /b 2
